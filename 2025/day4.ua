Trim ← ↘₁↘₋₁≡(↘₁↘₋₁)
Near ← Trim ⬚@. ⧈□ 3_3
Acc  ← × ≡≡◇(<4 /+ =@@ ⊂⊃↙₄↙₋₄♭)
Rm   ← ≡≡⨬(∘|⋅@.)
Iter ← /+/+ ⟜Rm Acc Near⤙=@@ ⊸∘ ◌
Loop ← ◌ ⍢(Iter|⊸>0) 1 ⊸∘
Diff ← - ∩/+ ∩/+ ∩(=@@)

TD ← ["..@@.@@@@."
      "@@@.@.@.@@"
      "@@@@@.@.@@"
      "@.@@@@..@."
      "@@.@@@@.@@"
      ".@@@@@@@.@"
      ".@.@.@.@@@"
      "@.@@@.@@@@"
      ".@@@@@@@@."
      "@.@.@@@.@."]

˙⍤ =13 /+/+ Acc Near ⤙=@@ TD
˙⍤ =43 Diff Loop TD

&fras "inputs/2025/day4.txt"
⊸∘ ⊜∘ ⊸≠ @\n

&p /+/+ Acc Near ⤙=@@
&p Diff Loop
