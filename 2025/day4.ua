NEAR ← ⬚@. ⧈□ 3_3
TRIM ← ↘₁↘₋₁≡(↘₁↘₋₁)
ACC  ← ×≡≡◇(<4/+=@@⊂⊃↙₄↙₋₄♭)
RM   ← ≡≡(⨬(∘|⋅@.)∩°□°⊟) ≡≡⊟∩≡≡□
ITER ← /+/+ ⟜RM ACC TRIM NEAR⤙=@@ ⊸∘ ◌
DIFF ← - ∩/+∩/+ ∩(=@@)

TD ← [
  "..@@.@@@@."
  "@@@.@.@.@@"
  "@@@@@.@.@@"
  "@.@@@@..@."
  "@@.@@@@.@@"
  ".@@@@@@@.@"
  ".@.@.@.@@@"
  "@.@@@.@@@@"
  ".@@@@@@@@."
  "@.@.@@@.@."
]

˙⍤ =13 /+/+ ACC TRIM NEAR ⤙=@@ TD

◌⍢(ITER|⊸>0)1 ⊸∘ TD
˙⍤ =43 DIFF

&fras "inputs/2025/day4.txt"
⊜∘ ⊸≠ @\n
&p /+/+ ACC TRIM NEAR⤙=@@

&fras "inputs/2025/day4.txt"
⊜∘ ⊸≠ @\n
◌⍢(ITER|⊸>0)1 ⊸∘
&p DIFF
